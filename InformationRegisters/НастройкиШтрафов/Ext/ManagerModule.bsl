&Вместо("ПолучитьТаблицуРазрешенногоВремениПосещения")
Функция PocketKey_ПолучитьТаблицуРазрешенногоВремениПосещения(Знач СтруктурнаяЕдиница, Знач Клиент, Знач Ключ, Знач ДанныеПоВходу)

	ТекущаяДата            = НачалоДня(ТекущаяДатаСеанса());
	ТекущееВремя           = Дата(1,1,1, Час(ДанныеПоВходу.ДатаПрихода), Минута(ДанныеПоВходу.ДатаПрихода), 00);
	ДеньНедели             = ДеньНедели(ТекущаяДата);
	ЭтоРабочийДень 		   = ОбщегоНазначения.ЭтоРабочийДень(ТекущаяДата);
	ВремяПереодеванияДо    = Константы.ВремяПереодеванияДоЗанятия.Получить() * 60;
	ВремяПереодеванияПосле = Константы.ВремяПереодеванияПослеЗанятия.Получить() * 60;

	ВремяРаботы 	= Справочники.СтруктурныеЕдиницы.ВремяРаботыСтруктурнойЕдиницы(СтруктурнаяЕдиница,  ДеньНедели);
	НачалоРаботы	= ?(ВремяРаботы = Неопределено, Дата(1,1,1,00,00,00), ВремяРаботы.НачалоРаботы);
	ОкончаниеРаботы = ?(ВремяРаботы = Неопределено, Дата(1,1,1,23,59,59), ВремяРаботы.ОкончаниеРаботы);

	МассивТипов = Новый Массив;
	МассивТипов.Добавить(Тип("ДокументСсылка.ЧленствоПакетУслуг"));
	МассивТипов.Добавить(Тип("ДокументСсылка.Реализация"));

	ТаблицаДоступов = Новый ТаблицаЗначений;
	ТаблицаДоступов.Колонки.Добавить("ЧленствоПакетУслуг", Новый ОписаниеТипов(МассивТипов));
	ТаблицаДоступов.Колонки.Добавить("ВремяНачало"		 , Новый ОписаниеТипов("Дата"));
	ТаблицаДоступов.Колонки.Добавить("ВремяКонца" 		 , Новый ОписаниеТипов("Дата"));
	ТаблицаДоступов.Колонки.Добавить("Контрагент" 		 , Новый ОписаниеТипов("СправочникСсылка.Контрагенты"));
	ТаблицаДоступов.Колонки.Добавить("Помещение"  		 , Новый ОписаниеТипов("СправочникСсылка.Помещения"));

	Если ЗначениеЗаполнено(Ключ) И ТипЗнч(ДанныеПоВходу.Основание) = Тип("ДокументСсылка.ЧленствоПакетУслуг")
		И ДанныеПоВходу.Основание.ВремяПосещенияНеБолее > 0 Тогда //Расчитываем время входа и выхода конкретно для этого ключа

		ИтоговаяТаблица = ТаблицаДоступов.СкопироватьКолонки("ВремяНачало, ВремяКонца, ЧленствоПакетУслуг");
		НоваяСтрока 	= ИтоговаяТаблица.Добавить();

		// Если клиент еще не вошел, то он может это сделать до конца работы стр единицы.
		Если НЕ ЗначениеЗаполнено(ДанныеПоВходу.ДатаПрихода) Тогда
			НоваяСтрока.ВремяНачало = ТекущаяДатаСеанса();
			НоваяСтрока.ВремяКонца  = ПолучитьВремяКонцаИнтервала(ТекущаяДата, ОкончаниеРаботы);
			Возврат ИтоговаяТаблица;
		КонецЕсли;

		ВремяВхода = НачалоМинуты(?(ЗначениеЗаполнено(ДанныеПоВходу.ДатаПрихода), ДанныеПоВходу.ДатаПрихода, ДанныеПоВходу.Период));

		Запрос = Новый Запрос(
		"ВЫБРАТЬ РАЗРЕШЕННЫЕ
		|	ЧленстваПакетыУслугОстатки.Основание.Контрагент КАК Контрагент,
		|	ЧленстваПакетыУслугОстатки.КоличествоОстаток КАК Остаток
		|ИЗ
		|	РегистрНакопления.ЧленстваПакетыУслуг.Остатки(
		|			&ТекущаяДата,
		|			Основание = &Основание
		|				И УслугаСегмент = НЕОПРЕДЕЛЕНО) КАК ЧленстваПакетыУслугОстатки");

		Запрос.УстановитьПараметр("Основание"  , ДанныеПоВходу.Основание);
		Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());

		Результат = Запрос.Выполнить().Выгрузить();

		КолчествоПосещений 	  = ?(Результат.Количество() > 0, Результат[0].Остаток + 1, 1); // Делаем +1, потому что при входе в клуб одно посещение списывается, но его надо учитывать.
		ДлительностьПосещения = ДанныеПоВходу.Основание.ВремяПосещенияНеБолее * 60 * КолчествоПосещений;
		ДлительностьПосещения = ДлительностьПосещения + ВремяПереодеванияДо + ВремяПереодеванияПосле;

		Если Не ЗначениеЗаполнено(ДанныеПоВходу.ДатаПрихода) Тогда //Клиент еще не вошел в зону тарификации			
			ДлительностьПосещения = ?(ДлительностьПосещения < 3600, 3600, ДлительностьПосещения);
		ИначеЕсли ДанныеПоВходу.Основание.ПребываниеВЗонеТарификации Тогда //Если в пакете учитывается чистое время пребывания в зоне тарификации

			//Определяем сколько чистого времени находился клиент в зоне тарификации
			Если ОбщегоНазначения.МодульСКУДСуществует() Тогда
				ЧистоеВремяПрибывания = ОбщегоНазначения.ПолучитьМодульСКУД().ВернутьЧистоеВремяПребывания(Ключ, ДанныеПоВходу.ДатаПрихода, ДанныеПоВходу.Основание);
			КонецЕсли;

			//корректируем его время выхода
			Если Не ЗначениеЗаполнено(ДанныеПоВходу.ДатаУхода) Тогда

				ВремяВхода = ДанныеПоВходу.ДатаПрихода;

				ОбщееВремяПрибывания  = ТекущаяДатаСеанса() - ВремяВхода;
				ЧистоеВремяПрибывания = ?(ЧистоеВремяПрибывания = Неопределено, ОбщееВремяПрибывания, ЧистоеВремяПрибывания);
				ДобавочноеВремя 	  = ОбщееВремяПрибывания - ЧистоеВремяПрибывания;
				ДлительностьПосещения = ДлительностьПосещения + ДобавочноеВремя;

			Иначе
				//Проверяем осталось ли время у клиента
				ЧистоеВремяПрибывания = ?(ЧистоеВремяПрибывания = Неопределено, ДлительностьПосещения, ЧистоеВремяПрибывания);

				Если ЧистоеВремяПрибывания >= ДлительностьПосещения Тогда //Клиент пересидел отведенное время
					ОбщееВремяПрибывания  = ДанныеПоВходу.ДатаУхода - ВремяВхода;
					ДобавочноеВремя 	  = ОбщееВремяПрибывания - ЧистоеВремяПрибывания;
					ДлительностьПосещения = ДлительностьПосещения + ДобавочноеВремя;
				Иначе
					ДлительностьПосещения = ?(ДлительностьПосещения < 3600, 3600, ДлительностьПосещения);
				КонецЕсли;

			КонецЕсли;

		КонецЕсли;  

		ВремяВыхода = ВремяВхода + ДлительностьПосещения;

		СтруктураВремениДействия 	 = Документы.ЧленствоПакетУслуг.ПолучитьСтруктуруОграниченийВремениДействия(ДанныеПоВходу.Основание);
		ОграничениеПоВремениДействия = СтруктураВремениДействия.ОграничениеПоВремениДействия;
		ТаблицаВремяДействия 		 = СтруктураВремениДействия.ВремяДействия;

		Если ОграничениеПоВремениДействия = Перечисления.ТипыОграниченийЧленствПакетовУслуг.ВремяОграничено Тогда

			Для каждого СтрокаТЧ Из ТаблицаВремяДействия Цикл

				ВремяВходаБезДаты  = Дата(1,1,1) + (ВремяВхода - НачалоДня(ВремяВхода));
				ВремяВыходаБезДаты = Дата(1,1,1) + (ВремяВыхода - НачалоДня(ВремяВыхода));

				Если ВремяДействияСовпадаетСТекущейДатой(СтрокаТЧ.ДеньНедели, ВремяВхода, ЭтоРабочийДень)
					И СтрокаТЧ.ВремяС <= ВремяВходаБезДаты
					И СтрокаТЧ.ВремяПо > ВремяВходаБезДаты
					И СтрокаТЧ.ВремяПо < ВремяВыходаБезДаты Тогда

					ВремяВыхода = НачалоДня(ВремяВыхода) + (СтрокаТЧ.ВремяПо - НачалоДня(СтрокаТЧ.ВремяПо));
				КонецЕсли;

			КонецЦикла;

		КонецЕсли;

		НоваяСтрока.ВремяНачало 	   = ВремяВхода;
		НоваяСтрока.ВремяКонца  	   = ВремяВыхода;
		НоваяСтрока.ЧленствоПакетУслуг = ДанныеПоВходу.Основание;

		Возврат ИтоговаяТаблица;

	КонецЕсли;

	ТаблицаЧленств = ПолучитьТаблицуЧленств(Клиент);

	Для Каждого СтрокаЧленство Из ТаблицаЧленств Цикл

		МассивДоступа = Новый Массив;

		Если СтрокаЧленство.ОграниченияПоВремениДействияИзменены ИЛИ СтрокаЧленство.ВремяДействия.Количество() > 0 Тогда
			ОграничениеПоВремениДействия = СтрокаЧленство.ОграничениеПоВремениДействия;
			ТаблицаВремяДействия 		 = СтрокаЧленство.ВремяДействия;
		Иначе
			ОграничениеПоВремениДействия = СтрокаЧленство.НоменклатураОграничениеПоВремениДействия;
			ТаблицаВремяДействия 		 = СтрокаЧленство.НоменклатураВремяДействия;
		КонецЕсли;

		Если ОграничениеПоВремениДействия = Перечисления.ТипыОграниченийЧленствПакетовУслуг.ВремяОграничено Тогда

			Для каждого СтрокаТЧ Из ТаблицаВремяДействия Цикл

				Если ВремяДействияСовпадаетСТекущейДатой(СтрокаТЧ.ДеньНедели, ТекущаяДата, ЭтоРабочийДень)
					И СтрокаТЧ.ВремяС <= ТекущееВремя
					И СтрокаТЧ.ВремяПо > ТекущееВремя Тогда

					ВремяНачала 	 = НачалоДня(ТекущаяДата) + Час(СтрокаТЧ.ВремяС) * 3600 + Минута(СтрокаТЧ.ВремяС) * 60;
					СтруктураДоступа = ПолучитьСтруктуруДоступа(СтрокаЧленство, ВремяНачала, ПолучитьВремяКонцаИнтервала(ТекущаяДата, СтрокаТЧ.ВремяПо));

					МассивДоступа.Добавить(СтруктураДоступа);

				КонецЕсли;

			КонецЦикла;

		Иначе

			Если ЗначениеЗаполнено(СтрокаЧленство.ВремяПосещенияНеБолее) Тогда

				//Определяем вход клиента, если его нет пишем текущую дату
				Посещение   = Документы.Посещение.ДанныеПоследнегоВходаКлиента(Клиент,, СтруктурнаяЕдиница);
				ВремяВыхода = СтрокаЧленство.Остаток * СтрокаЧленство.ВремяПосещенияНеБолее * 60;

				Если Посещение <> Неопределено И Не ЗначениеЗаполнено(Посещение.ДатаУхода) Тогда

					ВремяНачалоЧленство = НачалоДня(ТекущаяДата)+ Час(НачалоРаботы) * 3600 + Минута(НачалоРаботы) * 60;
					ВремяКонцаЧленство  = Посещение.ДатаПрихода + ВремяВыхода + ВремяПереодеванияПосле;
					СтруктураДоступа    = ПолучитьСтруктуруДоступа(СтрокаЧленство, ВремяНачалоЧленство, ВремяКонцаЧленство);

					МассивДоступа.Добавить(СтруктураДоступа);

				КонецЕсли;

			Иначе

				ВремяНачала 	 = НачалоДня(ТекущаяДата) + Час(НачалоРаботы) * 3600 + Минута(НачалоРаботы) * 60;
				СтруктураДоступа = ПолучитьСтруктуруДоступа(СтрокаЧленство, ВремяНачала, ПолучитьВремяКонцаИнтервала(ТекущаяДата, ОкончаниеРаботы));

				МассивДоступа.Добавить(СтруктураДоступа);

			КонецЕсли;

		КонецЕсли;

		ПроверитьНаОграничениеПоКоличествуПосещений(СтрокаЧленство, ТекущаяДата, МассивДоступа, ТаблицаДоступов);

	КонецЦикла; 

	ДатаСреза = ?(НачалоДня(ТекущаяДата - ВремяПереодеванияПосле) < НачалоДня(ТекущаяДата), ТекущаяДата, ТекущаяДата - ВремяПереодеванияПосле);

	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	// ++ ЛАА
	|	Занятия.Ссылка.Номенклатура КАК Услуга,
	// -- ЛАА
	|	Занятия.Контрагент КАК Контрагент,
	|	Занятия.ОснованиеОплаты КАК ЧленствоПакетУслуг,
	|	Занятия.Ссылка.ДатаВремяНачала КАК ВремяНачало,
	|	Занятия.Ссылка.ДатаВремяОкончания КАК ВремяКонца,
	|	Занятия.Ссылка.Помещение КАК Помещение
	|ИЗ
	|	Документ.Занятие.СоставЗанятия КАК Занятия
	|ГДЕ
	|	Занятия.Ссылка.ДатаВремяОкончания > &Дата
	|	И (Занятия.Ссылка.СтатусЗанятия = ЗНАЧЕНИЕ(Перечисление.СтатусыЗанятия.Запланировано)
	|			ИЛИ Занятия.Ссылка.СтатусЗанятия = ЗНАЧЕНИЕ(Перечисление.СтатусыЗанятия.Проводится)
	|			ИЛИ Занятия.Ссылка.СтатусЗанятия = ЗНАЧЕНИЕ(Перечисление.СтатусыЗанятия.Проведено))
	|	И Занятия.Ссылка.Проведен
	|	И НАЧАЛОПЕРИОДА(Занятия.Ссылка.ДатаВремяОкончания, ДЕНЬ) = НАЧАЛОПЕРИОДА(&Дата, ДЕНЬ)
	|	И ВЫБОР
	|			КОГДА &Клиент = НЕОПРЕДЕЛЕНО
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ Занятия.Контрагент = &Клиент
	|		КОНЕЦ");

	Запрос.УстановитьПараметр("Клиент", Клиент);
	Запрос.УстановитьПараметр("Дата"  , ДатаСреза);

	ТаблицаЗанятий = Запрос.Выполнить().Выгрузить();

	Для Каждого СтрокаТЧ Из ТаблицаЗанятий Цикл
		НоваяСтрока = ТаблицаДоступов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЧ);
		НоваяСтрока.ВремяНачало = СтрокаТЧ.ВремяНачало - ВремяПереодеванияДо;
		НоваяСтрока.ВремяКонца  = СтрокаТЧ.ВремяКонца + ВремяПереодеванияПосле;	
		// ++ ЛАА
		// Альтернативное время для переодевания
		Если УслугаЗанятияВСегментеДобавленияВремени(СтрокаТЧ.Услуга, 1) Тогда
			НоваяСтрока.ВремяНачало = СтрокаТЧ.ВремяНачало - (30*60); //30 мин 
			НоваяСтрока.ВремяКонца  = СтрокаТЧ.ВремяКонца + (3600*3); //добавим 3 часа 
		КонецЕсли;		
		Если УслугаЗанятияВСегментеДобавленияВремени(СтрокаТЧ.Услуга, 2) Тогда
			НоваяСтрока.ВремяНачало = СтрокаТЧ.ВремяНачало - (30*60); //30 мин 
			НоваяСтрока.ВремяКонца  = СтрокаТЧ.ВремяКонца + (30*60); //30 мин
		КонецЕсли;	
		// -- ЛАА
	КонецЦикла;

	ТаблицаГрупповыхЗанятий = ПолучитьТаблицуГрупповыхЗанятий(Клиент, ТекущаяДата);

	Для Каждого СтрокаЧленство Из ТаблицаГрупповыхЗанятий Цикл

		Если СтрокаЧленство.ОграниченияПоВремениДействияИзменены ИЛИ СтрокаЧленство.ВремяДействия.Количество() > 0 Тогда
			ОграничениеПоВремениДействия = СтрокаЧленство.ОграничениеПоВремениДействия;
			ТаблицаВремяДействия 		 = СтрокаЧленство.ВремяДействия;
		Иначе
			ОграничениеПоВремениДействия = СтрокаЧленство.НоменклатураОграничениеПоВремениДействия;
			ТаблицаВремяДействия 		 = СтрокаЧленство.НоменклатураВремяДействия;
		КонецЕсли;

		МассивДоступа = Новый Массив;
		Если ОграничениеПоВремениДействия = Перечисления.ТипыОграниченийЧленствПакетовУслуг.ВремяОграничено Тогда
			Для Каждого СтрокаТЧ Из ТаблицаВремяДействия Цикл

				Если ВремяДействияСовпадаетСТекущейДатой(СтрокаТЧ.ДеньНедели, ТекущаяДата, ЭтоРабочийДень)
					И СтрокаТЧ.ВремяПо >= Дата(1,1,1, Час(СтрокаЧленство.ВремяКонца), Минута(СтрокаЧленство.ВремяКонца), 00) Тогда

					ВремяНачала 	 = СтрокаЧленство.ВремяНачало - ВремяПереодеванияДо;
					ВремяКонца  	 = СтрокаЧленство.ВремяКонца + ВремяПереодеванияПосле;
					// ++ ЛАА
					// Альтернативное время для переодевания
					Если УслугаЗанятияВСегментеДобавленияВремени(СтрокаТЧ.Услуга, 1) Тогда
						ВремяНачала = СтрокаЧленство.ВремяНачало - (30*60); //30 мин 
						ВремяКонца  = СтрокаЧленство.ВремяКонца + (3600*3); //добавим 3 часа 
					КонецЕсли;		
					Если УслугаЗанятияВСегментеДобавленияВремени(СтрокаТЧ.Услуга, 2) Тогда
						ВремяНачала = СтрокаЧленство.ВремяНачало - (30*60); //30 мин 
						ВремяКонца  = СтрокаЧленство.ВремяКонца + (30*60); //30 мин
					КонецЕсли;	
					// -- ЛАА
					СтруктураДоступа = ПолучитьСтруктуруДоступа(СтрокаЧленство, ВремяНачала, ВремяКонца, СтрокаЧленство.Помещение);

					МассивДоступа.Добавить(СтруктураДоступа);

				КонецЕсли;

			КонецЦикла;
		Иначе

			ВремяНачала 	 = СтрокаЧленство.ВремяНачало - ВремяПереодеванияДо;
			ВремяКонца  	 = СтрокаЧленство.ВремяКонца + ВремяПереодеванияПосле;
			// ++ ЛАА
			// Альтернативное время для переодевания
			Если УслугаЗанятияВСегментеДобавленияВремени(СтрокаТЧ.Услуга, 1) Тогда
				ВремяНачала = СтрокаЧленство.ВремяНачало - (30*60); //30 мин 
				ВремяКонца  = СтрокаЧленство.ВремяКонца + (3600*3); //добавим 3 часа 
			КонецЕсли;		
			Если УслугаЗанятияВСегментеДобавленияВремени(СтрокаТЧ.Услуга, 2) Тогда
				ВремяНачала = СтрокаЧленство.ВремяНачало - (30*60); //30 мин 
				ВремяКонца  = СтрокаЧленство.ВремяКонца + (30*60); //30 мин
			КонецЕсли;	
			// -- ЛАА
			СтруктураДоступа = ПолучитьСтруктуруДоступа(СтрокаЧленство, ВремяНачала, ВремяКонца, СтрокаЧленство.Помещение);

			МассивДоступа.Добавить(СтруктураДоступа);

		КонецЕсли;

		ПроверитьНаОграничениеПоКоличествуПосещений(СтрокаЧленство, ТекущаяДата, МассивДоступа, ТаблицаДоступов);

	КонецЦикла;

	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Занятие.Номенклатура КАК Услуга,
	|	Занятие.ДатаВремяНачала КАК ВремяНачало,
	|	Занятие.ДатаВремяОкончания КАК ВремяКонца,
	|	Занятие.Помещение КАК Помещение
	|ПОМЕСТИТЬ ТаблицаЗанятий
	|ИЗ
	|	Документ.Занятие КАК Занятие
	|ГДЕ
	|	Занятие.ДатаВремяОкончания > &Дата
	|	И Занятие.СтатусЗанятия В (ЗНАЧЕНИЕ(Перечисление.СтатусыЗанятия.Запланировано), ЗНАЧЕНИЕ(Перечисление.СтатусыЗанятия.Проводится), ЗНАЧЕНИЕ(Перечисление.СтатусыЗанятия.Проведено))
	|	И Занятие.Проведен
	|	И Занятие.ТипЗанятия = ЗНАЧЕНИЕ(Перечисление.ТипыЗанятий.ГрупповоеЗанятие)
	|	И НАЧАЛОПЕРИОДА(Занятие.ДатаВремяОкончания, ДЕНЬ) = НАЧАЛОПЕРИОДА(&Дата, ДЕНЬ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЧленстваПакетыУслугОстаткиИОбороты.Основание.Контрагент КАК Контрагент,
	|	ЧленстваПакетыУслугОстаткиИОбороты.УслугаСегмент КАК Услуга,
	|	ЧленстваПакетыУслугОстаткиИОбороты.Основание КАК Основание
	|ПОМЕСТИТЬ ТаблицаПакетов
	|ИЗ
	|	РегистрНакопления.ЧленстваПакетыУслуг.ОстаткиИОбороты(
	|			,
	|			&ТекущаяДата,
	|			Авто,
	|			,
	|			Основание ССЫЛКА Документ.Реализация
	|				И &УслувиеПоКлиенту) КАК ЧленстваПакетыУслугОстаткиИОбороты
	|ГДЕ
	|	ЧленстваПакетыУслугОстаткиИОбороты.КоличествоКонечныйОстаток > 0
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ
	// ++ ЛАА
	|	ТаблицаЗанятий.Услуга КАК Услуга,
	// -- ЛАА
	|	ТаблицаЗанятий.ВремяНачало КАК ВремяНачало,
	|	ТаблицаЗанятий.ВремяКонца КАК ВремяКонца,
	|	ТаблицаЗанятий.Помещение КАК Помещение,
	|	ТаблицаПакетов.Контрагент КАК Контрагент,
	|	ТаблицаПакетов.Основание КАК ЧленствоПакетУслуг
	|ИЗ
	|	ТаблицаЗанятий КАК ТаблицаЗанятий
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаПакетов КАК ТаблицаПакетов
	|		ПО ТаблицаЗанятий.Услуга = ТаблицаПакетов.Услуга
	|ГДЕ
	|	НЕ ТаблицаПакетов.Контрагент ЕСТЬ NULL");

	Запрос.УстановитьПараметр("Клиент"	   , Клиент);
	Запрос.УстановитьПараметр("Дата"	   , ТекущаяДата);
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());

	Запрос.Текст = СтрЗаменить(Запрос.Текст , "&УслувиеПоКлиенту", ?(ЗначениеЗаполнено(Клиент), "ВЫРАЗИТЬ(Основание КАК Документ.Реализация).Контрагент = &Клиент", "ИСТИНА"));

	ТаблицаРазовых = Запрос.Выполнить().Выгрузить();

	Для Каждого СтрокаТЧ Из ТаблицаРазовых Цикл
		НоваяСтрока = ТаблицаДоступов.Добавить();
		ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЧ);
		НоваяСтрока.ВремяНачало = СтрокаТЧ.ВремяНачало - ВремяПереодеванияДо;
		НоваяСтрока.ВремяКонца  = СтрокаТЧ.ВремяКонца + ВремяПереодеванияПосле;
		// ++ ЛАА
		// Альтернативное время для переодевания
		Если УслугаЗанятияВСегментеДобавленияВремени(СтрокаТЧ.Услуга, 1) Тогда
			НоваяСтрока.ВремяНачало = СтрокаТЧ.ВремяНачало - (30*60); //30 мин 
			НоваяСтрока.ВремяКонца  = СтрокаТЧ.ВремяКонца + (3600*3); //добавим 3 часа 
		КонецЕсли;		
		Если УслугаЗанятияВСегментеДобавленияВремени(СтрокаТЧ.Услуга, 2) Тогда
			НоваяСтрока.ВремяНачало = СтрокаТЧ.ВремяНачало - (30*60); //30 мин 
			НоваяСтрока.ВремяКонца  = СтрокаТЧ.ВремяКонца + (30*60); //30 мин
		КонецЕсли;	
		// -- ЛАА
	КонецЦикла;

	мТаблицаДоступов = ТаблицаДоступов.СкопироватьКолонки("ВремяНачало, ВремяКонца, ЧленствоПакетУслуг");

	Для Каждого СтрокаТЧ Из ТаблицаДоступов Цикл

		Если СтрокаТЧ.ЧленствоПакетУслуг = ДанныеПоВходу.Основание ИЛИ СтрокаТЧ.ЧленствоПакетУслуг = ДанныеПоВходу.ОснованиеДополнительное Тогда
			НоваяСтрока = мТаблицаДоступов.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, СтрокаТЧ);
		КонецЕсли;

	КонецЦикла;

	мТаблицаДоступов.Сортировать("ВремяНачало Возр");
	ИтоговаяТаблица = мТаблицаДоступов.СкопироватьКолонки();

	Если мТаблицаДоступов.Количество() > 0 Тогда

		максВремяНачало = мТаблицаДоступов[0].ВремяНачало;
		максВремяКонца  = мТаблицаДоступов[0].ВремяКонца;

		Для Каждого СтрокаТЧ Из мТаблицаДоступов Цикл

			Если максВремяНачало <= СтрокаТЧ.ВремяНачало И максВремяКонца >= СтрокаТЧ.ВремяНачало Тогда
				максВремяКонца = Макс(СтрокаТЧ.ВремяКонца, максВремяКонца);
			Иначе

				НоваяСтрока = ИтоговаяТаблица.Добавить();
				НоваяСтрока.ВремяНачало 	   = максВремяНачало;
				НоваяСтрока.ВремяКонца  	   = максВремяКонца;
				НоваяСтрока.ЧленствоПакетУслуг = СтрокаТЧ.ЧленствоПакетУслуг;

				максВремяНачало = СтрокаТЧ.ВремяНачало;
				максВремяКонца  = СтрокаТЧ.ВремяКонца;

			КонецЕсли;

		КонецЦикла;

		НоваяСтрока = ИтоговаяТаблица.Добавить();
		НоваяСтрока.ВремяНачало 	   = максВремяНачало;
		НоваяСтрока.ВремяКонца  	   = максВремяКонца;
		НоваяСтрока.ЧленствоПакетУслуг = мТаблицаДоступов[0].ЧленствоПакетУслуг;

	КонецЕсли;

	Возврат ИтоговаяТаблица;

КонецФункции

&Вместо("ПолучитьТаблицуГрупповыхЗанятий")
Функция PocketKey_ПолучитьТаблицуГрупповыхЗанятий(Клиент, ТекущаяДата)

	УстановитьПривилегированныйРежим(Истина);

	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ЗанятиеСоставЗанятия.Ссылка.Номенклатура КАК Услуга,
	|	ЗанятиеСоставЗанятия.Ссылка.ДатаВремяНачала КАК ВремяНачало,
	|	ЗанятиеСоставЗанятия.Ссылка.ДатаВремяОкончания КАК ВремяКонца,
	|	ЗанятиеСоставЗанятия.Ссылка.Помещение КАК Помещение
	|ПОМЕСТИТЬ ТаблицаЗанятий
	|ИЗ
	|	Документ.Занятие.СоставЗанятия КАК ЗанятиеСоставЗанятия
	|ГДЕ
	|	ВЫБОР
	|			КОГДА &Клиент = НЕОПРЕДЕЛЕНО
	|				ТОГДА ИСТИНА
	|			ИНАЧЕ ЗанятиеСоставЗанятия.Контрагент = &Клиент
	|		КОНЕЦ
	|	И ЗанятиеСоставЗанятия.Ссылка.ДатаВремяОкончания > &Дата
	|	И ЗанятиеСоставЗанятия.Ссылка.СтатусЗанятия В (ЗНАЧЕНИЕ(Перечисление.СтатусыЗанятия.Запланировано), ЗНАЧЕНИЕ(Перечисление.СтатусыЗанятия.Проводится), ЗНАЧЕНИЕ(Перечисление.СтатусыЗанятия.Проведено))
	|	И ЗанятиеСоставЗанятия.Ссылка.Проведен
	|	И ЗанятиеСоставЗанятия.Ссылка.ТипЗанятия = ЗНАЧЕНИЕ(Перечисление.ТипыЗанятий.ГрупповоеЗанятие)
	|	И НАЧАЛОПЕРИОДА(ЗанятиеСоставЗанятия.Ссылка.ДатаВремяОкончания, ДЕНЬ) = НАЧАЛОПЕРИОДА(&Дата, ДЕНЬ)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	СтатусыЧленствПакетовУслуг.ЧленствоПакетУслуг КАК ЧленствоПакетУслуг
	|ПОМЕСТИТЬ АктивныеЧленстваПакеты
	|ИЗ
	|	РегистрСведений.СтатусыЧленствПакетовУслуг.СрезПоследних(
	|			&ТекущаяДата,
	|			&УслувиеПоКлиенту
	|				И НЕ ЧленствоПакетУслуг.ВидЧленстваПакетаУслуг = ЗНАЧЕНИЕ(Перечисление.ВидыЧленствПакетовУслуг.ОграниченПосещениями)
	|				И НЕ ЧленствоПакетУслуг.РезервироватьЗанятия
	|				И ЧленствоПакетУслуг.ТипЧленстваПакетаУслуг = ЗНАЧЕНИЕ(Перечисление.ТипыНоменклатуры.АбонементПакетУслуг)) КАК СтатусыЧленствПакетовУслуг
	|ГДЕ
	|	СтатусыЧленствПакетовУслуг.Статус = ЗНАЧЕНИЕ(Перечисление.СтатусыЧленствПакетовУслуг.Активен)
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	ВЫРАЗИТЬ(ЧленстваПакетыУслугОстаткиИОбороты.Основание КАК Документ.ЧленствоПакетУслуг) КАК Основание,
	|	ВЫБОР
	|		КОГДА ЧленстваПакетыУслугОстаткиИОбороты.УслугаСегмент ССЫЛКА Справочник.Номенклатура
	|			ТОГДА ЧленстваПакетыУслугОстаткиИОбороты.УслугаСегмент
	|		ИНАЧЕ СоставСегментов.ОбъектСегмента
	|	КОНЕЦ КАК Услуга,
	|	ВЫРАЗИТЬ(ЧленстваПакетыУслугОстаткиИОбороты.Основание КАК Документ.ЧленствоПакетУслуг).Контрагент КАК Контрагент
	|ПОМЕСТИТЬ ТаблицаПакетов
	|ИЗ
	|	РегистрНакопления.ЧленстваПакетыУслуг.ОстаткиИОбороты(
	|			,
	|			&ТекущаяДата,
	|			Авто,
	|			,
	|			&УслувиеПоКлиенту2
	|				И ВЫРАЗИТЬ(Основание КАК Документ.ЧленствоПакетУслуг) В
	|					(ВЫБРАТЬ
	|						АктивныеЧленстваПакеты.ЧленствоПакетУслуг
	|					ИЗ
	|						АктивныеЧленстваПакеты КАК АктивныеЧленстваПакеты)
	|				И НЕ УслугаСегмент = НЕОПРЕДЕЛЕНО) КАК ЧленстваПакетыУслугОстаткиИОбороты
	|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрСведений.СоставСегментов КАК СоставСегментов
	|		ПО ЧленстваПакетыУслугОстаткиИОбороты.УслугаСегмент = СоставСегментов.Сегмент
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	// ++ ЛАА
	|	ТаблицаЗанятий.Услуга КАК Услуга,
	// -- ЛАА
	|	ТаблицаЗанятий.ВремяНачало КАК ВремяНачало,
	|	ТаблицаЗанятий.ВремяКонца КАК ВремяКонца,
	|	ТаблицаЗанятий.Помещение КАК Помещение,
	|	ТаблицаПакетов.Контрагент КАК Контрагент,
	|	ТаблицаПакетов.Основание КАК ЧленствоПакетУслуг,
	|	ТаблицаПакетов.Основание.ПосещенийНеЧаще КАК ПосещенийНеЧаще,
	|	ТаблицаПакетов.Основание.ПосещенийНеЧащеТип КАК ПосещенийНеЧащеТип,
	|	ТаблицаПакетов.Основание.ОграничениеПоВремениДействия КАК ОграничениеПоВремениДействия,
	|	ТаблицаПакетов.Основание.ОграниченияПоВремениДействияИзменены КАК ОграниченияПоВремениДействияИзменены,
	|	ТаблицаПакетов.Основание.Номенклатура.ЧленствоПакетУслугОграничениеПоВремениДействия КАК НоменклатураОграничениеПоВремениДействия,
	|	ТаблицаПакетов.Основание.ВремяДействия.(
	|		ДеньНедели КАК ДеньНедели,
	|		ВремяС КАК ВремяС,
	|		ВремяПо КАК ВремяПо
	|	) КАК ВремяДействия,
	|	ТаблицаПакетов.Основание.Номенклатура.ЧленствоПакетУслугВремяДействия.(
	|		ДеньНедели КАК ДеньНедели,
	|		ВремяС КАК ВремяС,
	|		ВремяПо КАК ВремяПо
	|	) КАК НоменклатураВремяДействия
	|ИЗ
	|	ТаблицаЗанятий КАК ТаблицаЗанятий
	|		ЛЕВОЕ СОЕДИНЕНИЕ ТаблицаПакетов КАК ТаблицаПакетов
	|		ПО ТаблицаЗанятий.Услуга = ТаблицаПакетов.Услуга
	|ГДЕ
	|	НЕ ТаблицаПакетов.Контрагент ЕСТЬ NULL");

	Запрос.УстановитьПараметр("Клиент"	   , Клиент);
	Запрос.УстановитьПараметр("Дата"	   , ТекущаяДата);
	Запрос.УстановитьПараметр("ТекущаяДата", ТекущаяДатаСеанса());

	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УслувиеПоКлиенту2", ?(ЗначениеЗаполнено(Клиент), "ВЫРАЗИТЬ(Основание КАК Документ.ЧленствоПакетУслуг).Контрагент = &Клиент", "ИСТИНА"));
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УслувиеПоКлиенту", ?(ЗначениеЗаполнено(Клиент), "ЧленствоПакетУслуг.Контрагент = &Клиент", "ИСТИНА"));

	Возврат Запрос.Выполнить().Выгрузить();

КонецФункции

// Функция, проверяет состоит ли услуга в сегменте альтернативного времени.
//
Функция УслугаЗанятияВСегментеДобавленияВремени(Услуга, Сегмент = 1) Экспорт
	
	Если Сегмент = 1 Тогда
		лТекст = "
		|ВЫБРАТЬ
		|	СоставСегментов.Сегмент КАК Сегмент,
		|	СоставСегментов.ОбъектСегмента КАК ОбъектСегмента
		|ИЗ
		|	РегистрСведений.СоставСегментов КАК СоставСегментов
		|ГДЕ
		|	СоставСегментов.Сегмент.Наименование = ""Альтернативное время для переодевания""
		|	И СоставСегментов.ОбъектСегмента = &Услуга
		|";
	
	ИначеЕсли Сегмент = 2 Тогда
		лТекст = "
		|ВЫБРАТЬ
		|	СоставСегментов.Сегмент КАК Сегмент,
		|	СоставСегментов.ОбъектСегмента КАК ОбъектСегмента
		|ИЗ
		|	РегистрСведений.СоставСегментов КАК СоставСегментов
		|ГДЕ
		|	СоставСегментов.Сегмент.Наименование = ""Альтернативное время для переодевания (-30/+30)""
		|	И СоставСегментов.ОбъектСегмента = &Услуга
		|";
	
	КонецЕсли;
	
	лЗапрос = Новый Запрос(лТекст);
	
	// Установка параметров.
	лЗапрос.УстановитьПараметр("Услуга", Услуга);
	
	Возврат НЕ лЗапрос.Выполнить().Пустой();
		
КонецФункции

