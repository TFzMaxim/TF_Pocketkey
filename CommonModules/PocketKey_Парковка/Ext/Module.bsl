Функция ПолучитьДоступПарковка(Клиент, Карта, Дата, ЭтоВход, СтруктурнаяЕдиница) Экспорт
	
	СтруктураВозврат = Новый Структура("Доступ, Основание, Гость, Описание", Ложь, , Ложь, "");	
	
	// Отказ в проезде для сотрудников
	ЭтоСотрудникУК = Ложь;
	ТаблицаЧленств = PocketKey_Модуль.PocketKey_ПолучитьТаблицуЧленств(СтруктурнаяЕдиница, Клиент, Дата);
	Если ТипЗнч(Клиент) = Тип("СправочникСсылка.Контрагенты") Тогда		
		Для Каждого СтрокаЧленство из ТаблицаЧленств Цикл
			
			Если СтрНайти(СтрокаЧленство.Номенклатура.Наименование, "УК") > 0 Тогда
				
				ЭтоСотрудникУК = Истина;
				
				СтруктураВозврат.Доступ		= Истина;
				СтруктураВозврат.Основание	= СтрокаЧленство;
				СтруктураВозврат.Описание	= "сотрудник УК (terfit)";
				Возврат СтруктураВозврат;

			КонецЕсли;
			
			//++ЗМВ 01.10.2025
			Если СтрНайти(ВРег(СтрокаЧленство.Номенклатура.Наименование), ВРег("Парковка")) > 0 Тогда
				
				ЭтоСотрудникУК = Истина;
				
				СтруктураВозврат.Доступ		= Истина;
				СтруктураВозврат.Основание	= СтрокаЧленство;
				СтруктураВозврат.Описание	= "доступ только на парковку (terfit)";
				Возврат СтруктураВозврат;

			КонецЕсли;
			//--ЗМВ 01.10.2025

			Если Лев(СтрокаЧленство.Номенклатура.Наименование, 3) = "200" И НЕ ЭтоСотрудникУК Тогда	
				СтруктураВозврат.Доступ		= Ложь;
				СтруктураВозврат.Основание	= СтрокаЧленство;
				СтруктураВозврат.Описание	= "сотрудник (terfit)";
				Возврат СтруктураВозврат;
			КонецЕсли;
			
		КонецЦикла;
	ИначеЕсли ТипЗнч(Клиент) = Тип("СправочникСсылка.Сотрудники") Тогда
		СтруктураВозврат.Доступ		= Ложь;
		СтруктураВозврат.Описание	= "сотрудник (terfit)";
		Возврат СтруктураВозврат;
	КонецЕсли;
	
	///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
	СтруктураВозврат = МенеджерОборудованияСКУД_КОРП.ПолучитьДоступПарковка(Клиент, Карта, Дата, ЭтоВход, СтруктурнаяЕдиница);
	///////////////////////////////////////////////////////////////////////////////////////////////////////////////////////
	
	// Фиксируем въезд/выезд
	Если ЭтоВход Тогда
		
		Если СтруктураВозврат.Доступ Тогда		
			ДокументПарковкиВъезд = PocketKey_Парковка.ПолучитьДокументПарковкиДляВъезда(СтруктурнаяЕдиница, Клиент);
			Если ДокументПарковкиВъезд = Неопределено Тогда
				Документы.Парковка.СоздатьДокументПарковки(СтруктурнаяЕдиница, Клиент, Неопределено, Карта, Неопределено, Дата, Неопределено, "Парковка (terfit)");
			КонецЕсли;
		КонецЕсли;
		
		Возврат СтруктураВозврат;
		
	Иначе
		
		Документы.Парковка.ЗаписатьВыездСПарковки(СтруктурнаяЕдиница, Клиент, Дата, Неопределено, Карта, Неопределено);
		
		// Закрываем все не закрытые документы парковки
		НеЗакрытыеДокументыПарковкиЗаВесьПериод = PocketKey_Парковка.НайтиНеЗакрытыеДокументыПарковкиЗаВесьПериод(СтруктурнаяЕдиница, Клиент);
		Если НеЗакрытыеДокументыПарковкиЗаВесьПериод.Количество() > 0 Тогда
			Для Каждого Парковка Из НеЗакрытыеДокументыПарковкиЗаВесьПериод Цикл
				ПарковкаОбъект = Парковка.Ссылка.ПолучитьОбъект();
				ПарковкаОбъект.ДатаВыезда = Дата;
				ПарковкаОбъект.Записать(РежимЗаписиДокумента.Проведение);
			КонецЦикла;
		КонецЕсли;
		
		// Тарифицируем
		ЗакрытыеДокументыПарковкиЗаТекущийДень = PocketKey_Парковка.НайтиЗакрытыеДокументыПарковкиЗаТекущийДень(СтруктурнаяЕдиница, Клиент);
		Если ЗакрытыеДокументыПарковкиЗаТекущийДень.Количество() > 0 Тогда
			
			ВремяПрибыванияНаПарковкеЗаДень = 0;
			
			Для Каждого Парковка Из ЗакрытыеДокументыПарковкиЗаТекущийДень Цикл
				РазницаДат = Парковка.ДатаВыезда - Парковка.ДатаВъезда;
				ВремяПрибыванияНаПарковкеЗаДень = ВремяПрибыванияНаПарковкеЗаДень + РазницаДат; 
			КонецЦикла;
			
			КоличествоЗадержка = Окр(ВремяПрибыванияНаПарковкеЗаДень / 3600 + 0.5, 0, 0) - 3; // На сколько часов задержался клиент
			
			//++ ЗМВ 20.08.2025
			// В переменную помещается количество часов за которое уже выставили штраФ
			// Если штраф выставлен за превый час, то за повторный проезд через турникет парковки штраф не начисляется за этот час 
			ПревышеноЧасовНаПарковке = ПолучитьПоследнийВыставленныйШтраф(Клиент);
			//-- ЗМВ 20.08.2025
			
			СтруктурнаяЕдиницаСтрока = ВРег(Строка(ПараметрыСеанса.ТекущаяСтруктурнаяЕдиница));
			
			ТарификацияВключена = Ложь;
			
			#Область Тарификации_Парковки
			
			Если СтрНайти(СтруктурнаяЕдиницаСтрока, "ЛЮБЕРЦЫ") > 0 Тогда
				ТарификацияВключена = Истина;
			КонецЕсли;
			
			//++ ЗМВ 23.07.2025
			Если СтрНайти(СтруктурнаяЕдиницаСтрока, ВРег("КУРКИНО")) > 0 Тогда
				ТарификацияВключена = Истина;
			КонецЕсли; 
			//-- ЗМВ 23.07.2025
			
			#КонецОбласти
			                                                                                                               
			Если КоличествоЗадержка > 0 И НЕ ЭтоСотрудникУК И ТарификацияВключена И КоличествоЗадержка <> ПревышеноЧасовНаПарковке Тогда
				
				РеализацияСсылка = PocketKey_Парковка.ВыполнитьТарификациюПарковки(СтруктурнаяЕдиница, Клиент, КоличествоЗадержка);
				
				// При наличии документа реализации со штрафом попытаемся оплатить его
				Если РеализацияСсылка <> Неопределено Тогда
					ДокументОплачен = PocketKey_ПродажиОплаты.ОплатаСЛицевогоСчета(РеализацияСсылка);
					// И если оплата прошла то отправляем на кассовый сервер
					Если ДокументОплачен Тогда
						PocketKey_ПродажиОплаты.ОтправитьНаКассовыйСервер(РеализацияСсылка);
					КонецЕсли;
				КонецЕсли;
				
			КонецЕсли;
			
		КонецЕсли;
		
		СтруктураВозврат.Доступ = Истина;
		
	КонецЕсли;
	
	Возврат СтруктураВозврат;
	
КонецФункции

Функция ПолучитьДокументПарковкиДляВъезда(СтруктурнаяЕдиница, Клиент) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	Парковка.Ссылка КАК Ссылка,
	|	Парковка.ДатаВъезда КАК ДатаВъезда,
	|	Парковка.ДатаВыезда КАК ДатаВыезда
	|ИЗ
	|	Документ.Парковка КАК Парковка
	|ГДЕ
	|	Парковка.СтруктурнаяЕдиница = &СтруктурнаяЕдиница
	|	И &УсловиеТипОбъекта
	|	И Парковка.Клиент = &Клиент
	|	И Парковка.Проведен
	|	И НЕ Парковка.ПометкаУдаления
	|	И Парковка.Дата >= &Период
	|	И Парковка.ДатаВыезда = ДАТАВРЕМЯ(1,1,1)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Парковка.Дата УБЫВ");
	
	Запрос.УстановитьПараметр("Период"				, НачалоДня(ТекущаяДатаСеанса()));
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница"	, СтруктурнаяЕдиница);
	Запрос.УстановитьПараметр("Клиент"				, Клиент);
	
	Если ТипЗнч(Клиент) = Тип("СправочникСсылка.Контрагенты") Тогда
		УсловиеТипОбъекта = "Парковка.Клиент ССЫЛКА Справочник.Контрагенты";
	Иначе
		УсловиеТипОбъекта = "Парковка.Клиент ССЫЛКА Справочник.Сотрудники";
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеТипОбъекта", УсловиеТипОбъекта);
	
	Выборка = Запрос.Выполнить().Выбрать();
	
	Если Выборка.Следующий() Тогда
		
		ПарковкаОбъект = Выборка.Ссылка.ПолучитьОбъект();
		Возврат ПарковкаОбъект; 
		
	Иначе
		// Если нет открытой парковки, создаем новый документ.
		Возврат Неопределено;
	КонецЕсли;
	
КонецФункции

Функция НайтиЗакрытыеДокументыПарковкиЗаТекущийДень(СтруктурнаяЕдиница, Клиент) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Парковка.Ссылка КАК Ссылка,
	|	Парковка.ДатаВъезда КАК ДатаВъезда,
	|	Парковка.ДатаВыезда КАК ДатаВыезда
	|ИЗ
	|	Документ.Парковка КАК Парковка
	|ГДЕ
	|	Парковка.СтруктурнаяЕдиница = &СтруктурнаяЕдиница
	|	И &УсловиеТипОбъекта
	|	И Парковка.Клиент = &Клиент
	|	И Парковка.Проведен
	|	И НЕ Парковка.ПометкаУдаления
	|	И Парковка.Дата >= &Период
	|	И ДатаВыезда <> ДАТАВРЕМЯ(1,1,1)
	|
	|УПОРЯДОЧИТЬ ПО
	|	Парковка.Дата УБЫВ");
	
	Запрос.УстановитьПараметр("Период"				, НачалоДня(ТекущаяДатаСеанса()));
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница"	, СтруктурнаяЕдиница);
	Запрос.УстановитьПараметр("Клиент"				, Клиент);
	
	Если ТипЗнч(Клиент) = Тип("СправочникСсылка.Контрагенты") Тогда
		УсловиеТипОбъекта = "Парковка.Клиент ССЫЛКА Справочник.Контрагенты";
	Иначе
		УсловиеТипОбъекта = "Парковка.Клиент ССЫЛКА Справочник.Сотрудники";
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеТипОбъекта", УсловиеТипОбъекта);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция НайтиНеЗакрытыеДокументыПарковкиЗаВесьПериод(СтруктурнаяЕдиница, Клиент) Экспорт
	
	Запрос = Новый Запрос(
	"ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Парковка.Ссылка КАК Ссылка,
	|	Парковка.ДатаВъезда КАК ДатаВъезда,
	|	Парковка.ДатаВыезда КАК ДатаВыезда
	|ИЗ
	|	Документ.Парковка КАК Парковка
	|ГДЕ
	|	Парковка.СтруктурнаяЕдиница = &СтруктурнаяЕдиница
	|	И &УсловиеТипОбъекта
	|	И Парковка.Клиент = &Клиент
	|	И Парковка.Проведен
	|	И НЕ Парковка.ПометкаУдаления
	|   И Парковка.ДатаВыезда = ДАТАВРЕМЯ(1,1,1)
	|УПОРЯДОЧИТЬ ПО
	|	Парковка.Дата УБЫВ");
	
	Запрос.УстановитьПараметр("Период"				, НачалоДня(ТекущаяДатаСеанса()));
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница"	, СтруктурнаяЕдиница);
	Запрос.УстановитьПараметр("Клиент"				, Клиент);
	
	Если ТипЗнч(Клиент) = Тип("СправочникСсылка.Контрагенты") Тогда
		УсловиеТипОбъекта = "Парковка.Клиент ССЫЛКА Справочник.Контрагенты";
	Иначе
		УсловиеТипОбъекта = "Парковка.Клиент ССЫЛКА Справочник.Сотрудники";
	КонецЕсли;
	
	Запрос.Текст = СтрЗаменить(Запрос.Текст, "&УсловиеТипОбъекта", УсловиеТипОбъекта);
	
	Возврат Запрос.Выполнить().Выгрузить();
	
КонецФункции

Функция ВыполнитьТарификациюПарковки(СтруктурнаяЕдиница, Клиент, КоличествоЗадержка) Экспорт
		
	Услуга = Справочники.Номенклатура.НайтиПоНаименованию("Парковка 60 минут");	
	Цена = РегистрыСведений.ЦеныНоменклатуры.ПолучитьПоследнее(ТекущаяДата(), Новый Структура("Номенклатура, ВидЦен", Услуга, Справочники.ВидыЦен.Розничная)).Цена;
	
	Реализация = Документы.Реализация.СоздатьДокумент();
	Реализация.СтруктурнаяЕдиница = СтруктурнаяЕдиница;
	Реализация.Заполнить(Неопределено);
	Реализация.Организация 		  = СтруктурнаяЕдиница.Организация;
	Реализация.Контрагент  		  = Клиент;
	Реализация.Дата 	   		  = ТекущаяДата();
	Реализация.Комментарий        = "Pocketkey: Начисления за парковку. Документ создан автоматически";
	Реализация.Запасы.Очистить();
	
	НоваяСтрока 				   = Реализация.Запасы.Добавить();
	НоваяСтрока.Номенклатура 	   = Услуга;
	НоваяСтрока.Количество 		   = КоличествоЗадержка;
	НоваяСтрока.КоличествоУпаковок = КоличествоЗадержка;
	НоваяСтрока.Цена 			   = Цена;
	НоваяСтрока.СтавкаНДС 		   = ?(ЗначениеЗаполнено(Услуга.СтавкаНДС), Услуга.СтавкаНДС , Справочники.СтавкиНДС.БезНДС);
	НоваяСтрока.ВидЦен 			   = Справочники.ВидыЦен.Розничная;	
	
	ОбработкаТабличныхЧастейКлиент.РассчитатьСуммуТабЧасти(НоваяСтрока);
	ОбработкаТабличныхЧастейКлиент.РассчитатьСуммуНДСТабЧасти(НоваяСтрока, Реализация.СуммаВключаетНДС);
	ОбработкаТабличныхЧастейКлиент.РассчитатьСуммуВКолонкеВсегоТабЧасти(НоваяСтрока, ,Реализация.СуммаВключаетНДС);
	
	Реализация.Записать(РежимЗаписиДокумента.Проведение);
	
	Возврат Реализация.Ссылка;
	
КонецФункции

//++ ЗМВ 20.08.2025

// Возвращает количество часов за которые клиенту выставили штраф
//
// Параметры:
//  Клиент  - СправочникСсылка.Контрагент - Контрагент
//
// Возвращаемое значение:
//   Число   - Возвращает за какое количество часов выставлены штрафы
//
Функция ПолучитьПоследнийВыставленныйШтраф(Клиент)
	
	КоличествоЗадержка = 0;
	
	лТекст = "
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ
	|	Реализация.Ссылка КАК Документ,
	|	Реализация.Дата КАК Дата
	|ПОМЕСТИТЬ ВТ_ДокументРеализации
	|ИЗ
	|	Документ.Реализация КАК Реализация
	|ГДЕ
	|	Реализация.Контрагент = &Контрагент
	|	И Реализация.Проведен
	|	И Реализация.Дата МЕЖДУ &ДатаНачала И &ДатаОкончания
	|	И Реализация.Запасы.Номенклатура = &Номенклатура
	|;
	|
	|////////////////////////////////////////////////////////////////////////////////
	|ВЫБРАТЬ РАЗРЕШЕННЫЕ ПЕРВЫЕ 1
	|	ВТ_ДокументРеализации.Документ КАК Документ,
	|	РеализацияЗапасы.Номенклатура КАК Номенклатура,
	|	РеализацияЗапасы.Количество КАК Количество
	|ИЗ
	|	ВТ_ДокументРеализации КАК ВТ_ДокументРеализации
	|		ЛЕВОЕ СОЕДИНЕНИЕ Документ.Реализация.Запасы КАК РеализацияЗапасы
	|		ПО (ВТ_ДокументРеализации.Документ = РеализацияЗапасы.Ссылка)
	|
	|УПОРЯДОЧИТЬ ПО
	|	ВТ_ДокументРеализации.Дата УБЫВ
	|";
	
	Запрос = Новый Запрос(лТекст);
	Запрос.МенеджерВременныхТаблиц = Новый МенеджерВременныхТаблиц;
	
	// Присвоение значений переменным параметров.	
	Номенклатура = Справочники.Номенклатура.НайтиПоНаименованию("Парковка 60 минут");
	
	// Установка параметров.
	Запрос.УстановитьПараметр("ДатаНачала"   , 	НачалоДня(ТекущаяДата()));
	Запрос.УстановитьПараметр("ДатаОкончания", 	КонецДня(ТекущаяДата()));
	Запрос.УстановитьПараметр("Контрагент"   , 	Клиент);
	Запрос.УстановитьПараметр("Номенклатура" , 	Номенклатура);
	
	
	Выборка = Запрос.Выполнить().Выгрузить();
	
	Если Выборка.Количество() <> 0 Тогда	
		  КоличествоЗадержка = Выборка[0].Количество;
		  Возврат КоличествоЗадержка;
	  Иначе
		  Возврат КоличествоЗадержка;
	КонецЕсли;
	

КонецФункции 
//-- ЗМВ 20.08.2025
