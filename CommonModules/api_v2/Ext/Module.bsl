//++ ЗМВ 20.06.2025

//Функция записывает на групповое занятие
//
// client_id - УИД - УИД клиента строка
// appointment_id - УИД - УИД занятия строка
// СтруктурнаяЕдиница - Структурная единица - СправочникСсылка.СтруктурнаяЕдиница
//
Функция PocketKey_post_group_appointment_terfit(client_id, appointment_id, СтруктурнаяЕдиница)Экспорт 

	СтруктураОшибки = Неопределено;
	ОписаниеОшибки	= Неопределено;
	Клиент 			= ПолучитьСсылкуПоИдентификатору_v3(client_id	  , СтруктураОшибки, Истина, "Контрагенты", 1010, Истина);
	Занятие 		= ПолучитьСсылкуПоИдентификатору_v3(appointment_id, СтруктураОшибки, Ложь  , "Занятие"	  , 1012, Истина);
	
	НачатьТранзакцию();
	
	УстановитьБлокировкуЗанятия(Занятие);
	ТекстСообщения = "";
	
	ЗанятиеОбъект = Занятие.ПолучитьОбъект();
	
	// Если требуется основание для записи, а у клиента его нет, то записываем с бронью
	// Если не требуется основание то просто записывает, с онованием или без него.
	ТребуетсяОснование   	 = ЗанятиеОбъект.Номенклатура.ТребуетсяОснованиеДляЗаписи;
	ДоступноеОснованиеОплаты = api_ОбщегоНазначения.ПолучитьДоступноеОснованиеОказанияУслуги(, Клиент, Занятие);
	
	ДанныеЗанятия = Неопределено;	
	ТаблицаСоставаЗанятия = Неопределено;
	Документы.Занятие.ИнициализироватьТекущийСоставЗанятия(, ЗанятиеОбъект, ТаблицаСоставаЗанятия);		
	РегистрыСведений.ДанныеЗанятий.ИнициализироватьТекущиеДанныеЗанятия(, ЗанятиеОбъект, ДанныеЗанятия);
	
	СтрокаЗанятия = ТаблицаСоставаЗанятия.Найти(Клиент, "Контрагент");
		
	Если СтрокаЗанятия = Неопределено Тогда		
		СтрокаЗанятия 		 = ТаблицаСоставаЗанятия.Добавить();
		СтрокаЗанятия.ВидЦен = ПолучитьВидЦенСотрудникаПоСтруктурнойЕдинице(ЗанятиеОбъект.Сотрудник, ЗанятиеОбъект.СтруктурнаяЕдиница);
	КонецЕсли;
	
	СтрокаЗанятия.Контрагент 	 = Клиент;
	СтрокаЗанятия.Количество 	 = ДанныеЗанятия.КоличествоОказываемыхУслуг;
	СтрокаЗанятия.СтатусПрибытия = 1; // 0 не прибыл, 1 прибыл на занятие
	
	Статус = ЗаполнитьОснованиеОплатыИВернутьСтатусБронирования(СтрокаЗанятия, ДоступноеОснованиеОплаты, ТребуетсяОснование);
	
	Попытка
		ЗанятиеОбъект.ДополнительныеСвойства.Вставить("РасчетКонтрагента", Клиент);
		ПараметрыЗаписи = Неопределено;
		Документы.Занятие.ИнициализироватьПараметрыЗаписи(ПараметрыЗаписи, РежимЗаписиДокумента.Проведение, , , Истина);
		Документы.Занятие.ЗаписатьОбъект(ЗанятиеОбъект, ДанныеЗанятия, ТаблицаСоставаЗанятия, ПараметрыЗаписи);
		
		Отказ 				= ПараметрыЗаписи.ДанныеПроверки.ЗаписьЗапрещена;
		ОписаниеОграничения = ПараметрыЗаписи.ДанныеПроверки.ОписаниеОграничения;
	Исключение
		Отказ 				= Истина;
		ОписаниеОграничения	= ОписаниеОшибки();
	КонецПопытки;
	
	Если Отказ Тогда
		ОтменитьТранзакцию();
		ЗаписьЖурналаРегистрации("api_v2", 2003,,, ОписаниеОграничения);
		
		Возврат ПолучитьСтруктуруОшибки_v2(2003, ОписаниеОграничения);
	Иначе
		ЗафиксироватьТранзакцию();
		
		Возврат Новый Структура("result, data", Истина, ПолучитьСтруктуруЗанятия_v2(ЗанятиеОбъект.Ссылка, Статус, Клиент));
	КонецЕсли;
	
КонецФункции
//-- ЗМВ 20.06.2025