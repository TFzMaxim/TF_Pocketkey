Процедура ВыставитьШтрафКлиенту(ТекущийКлиент, ТекущаяСтруктурнаяЕдиница) Экспорт
	
	Обработки.РабочийСтол.ВыставитьШтрафКлиенту(ТекущийКлиент, Неопределено, ТекущаяСтруктурнаяЕдиница);
	
КонецПроцедуры

Функция ПолучитьСуммуДолга(ТекущийКлиент, ТекущаяСтруктурнаяЕдиница) Экспорт
	
	Возврат РегистрыНакопления.РасчетыСПокупателями.ПолучитьЗадолженностьПоКонтрагентуСУчетомРассрочек(ТекущийКлиент, ТекущаяДатаСеанса() + 1, ТекущаяСтруктурнаяЕдиница);
	                  
КонецФункции

Функция ПолучитьНеОплаченныйШтраф(Контрагент, СтруктурнаяЕдиница) Экспорт
	
	ТекстЗапроса =	"
					|ВЫБРАТЬ
					|	РеализацияЗапасы.Ссылка КАК Ссылка
					|ИЗ
					|	Документ.Реализация.Запасы КАК РеализацияЗапасы
					|ГДЕ
					|	РеализацияЗапасы.Номенклатура.Наименование ПОДОБНО ""Превышение времени пребывания в клубе""
					|	И РеализацияЗапасы.Ссылка.Контрагент = &Контрагент
					|	И РеализацияЗапасы.Ссылка.СтруктурнаяЕдиница = &СтруктурнаяЕдиница
					|	И РеализацияЗапасы.Ссылка.Проведен
					|	И НЕ РеализацияЗапасы.Ссылка.ПометкаУдаления
					|	И РеализацияЗапасы.Ссылка.НомерЧекаККМ = 0
					|	И РеализацияЗапасы.Ссылка.Дата >= НАЧАЛОПЕРИОДА(&Дата, День)
					|";
	
	Запрос = Новый Запрос(ТекстЗапроса);

	// Установка параметров.
	Запрос.УстановитьПараметр("Контрагент", Контрагент);
	Запрос.УстановитьПараметр("СтруктурнаяЕдиница", СтруктурнаяЕдиница);
	Запрос.УстановитьПараметр("Дата", ТекущаяДатаСервера());

	Выборка = Запрос.Выполнить().Выбрать();

	Пока Выборка.Следующий() Цикл
		
		СуммаДокумента = РегистрыНакопления.РасчетыСПокупателями.ПолучитьЗадолженностьПоДокументу(Выборка.Ссылка);
		
		Если СуммаДокумента > 0 Тогда
			
			Возврат Выборка.Ссылка;
			
		КонецЕсли;
		
	КонецЦикла;
	
	Возврат Неопределено;
         
КонецФункции

Функция ОплатаСЛицевогоСчета(Документ) Экспорт
	
	ДокументОбъект = Документ.ПолучитьОбъект();
	Дата = ТекущаяДатаСервера();
	
	ТаблицаЛС = РегистрыНакопления.РасчетыСПокупателями.ПолучитьОстаткиНаЛицевыхСчетахКонтрагента(Дата, ДокументОбъект.Организация, ДокументОбъект.Контрагент);
	ОстатокЛС = ТаблицаЛС.Итог("СуммаОстаток");
	
	СуммаКОплате = ДокументОбъект.Запасы.Итог("Всего");		
	
	КассаОплаты = Неопределено;
	Если КассовыйСервер.КассовыйСерверИспользуется(ДокументОбъект.Организация) Тогда
		КассаОплаты = КассовыйСерверПовтИсп.КассаКассовогоСервера(ДокументОбъект.Организация);
	Иначе
		Возврат Ложь;
	КонецЕсли;
	
	Если СуммаКОплате <= ОстатокЛС Тогда
		Для Каждого СтрокаТЧ Из ТаблицаЛС Цикл
			
			Если СуммаКОплате <= 0 Тогда
				Прервать;
			КонецЕсли;
			
			СуммаНаЛСБольшеСуммыКОплате = (СтрокаТЧ.СуммаОстаток >= СуммаКОплате);
			
			СуммаОплаты  = ?(СуммаНаЛСБольшеСуммыКОплате, СуммаКОплате, СтрокаТЧ.СуммаОстаток);
			СуммаКОплате = ?(СуммаНаЛСБольшеСуммыКОплате, 0, СуммаКОплате - СтрокаТЧ.СуммаОстаток);				
			
			СтрокаАванса = ДокументОбъект.УчтенныеАвансы.Добавить();
			СтрокаАванса.ТипАвансовыхСредств = Перечисления.ТипыДенежныхСредств.ЛицевойСчет;
			СтрокаАванса.Аванс               = СтрокаТЧ.ЛицевойСчет;
			СтрокаАванса.ВладелецАванса      = СтрокаТЧ.Контрагент;
			СтрокаАванса.КассаОплаты         = КассаОплаты;
			СтрокаАванса.ДатаПлатежа         = Дата;
			СтрокаАванса.СуммаАванса         = СуммаОплаты;
			
		КонецЦикла;
		
		ДокументОбъект.Записать(РежимЗаписиДокумента.Проведение);
		
		Возврат Истина;
		
	КонецЕсли;
	
	Возврат Ложь;
			
КонецФункции

Процедура ОтправитьНаКассовыйСервер(Документ) Экспорт
	
	api_ОбщегоНазначения.ДобавитьДокументОплатыВОчередьКассовогоСервера(Документ, "КассовыйСервер");
	
КонецПроцедуры
