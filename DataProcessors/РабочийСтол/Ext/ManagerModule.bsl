&Вместо("ПроверитьНаличиеЗадолженностеУКлиента")
Функция PocketKey_ПроверитьНаличиеЗадолженностеУКлиента(ТабЗадолженностиКлиента, ТекущийКлиент, Описание)

	//-Трегубов
	//ЯндексЕда - вход/выход с долгом
	Если ТипЗнч(ТекущийКлиент) = Тип("СправочникСсылка.Контрагенты") Тогда
		Если ТекущийКлиент.Наименование = "Яндекс Еда" Тогда
			Возврат Ложь;
		КонецЕсли;
	КонецЕсли;
	//-Трегубов
	
	МассивДокументов = Новый Массив;
	ВерхнийУровень = ТабЗадолженностиКлиента.ПолучитьЭлементы();
	НайтиОснованияВДеревеЗадолженностей(ВерхнийУровень, ТекущийКлиент, Тип("ДокументСсылка.Занятие"), Истина,МассивДокументов); 
	НайтиОснованияВДеревеЗадолженностей(ВерхнийУровень, ТекущийКлиент, Тип("ДокументСсылка.ОперацииСЧленствомПакетомУслуг"), Истина,МассивДокументов); 
	НайтиОснованияВДеревеЗадолженностей(ВерхнийУровень, ТекущийКлиент, Тип("ДокументСсылка.Аренда"), Истина,МассивДокументов); 

	Если МассивДокументов.Количество() > 0 Тогда
		Описание = Нстр("ru='Имеются незакрытые услуги или иные операции!'; en='There are unclosed services or other operations!'");
		Возврат Истина;	
	КонецЕсли;

	МассивДокументов.Очистить();
	НайтиОснованияВДеревеЗадолженностей(ВерхнийУровень, ТекущийКлиент, Тип("ДокументСсылка.Реализация"),Истина, МассивДокументов); 

	Если МассивДокументов.Количество() > 0 Тогда
		Описание = Нстр("ru='Имеются неоплаченные услуги или товары!'; en='Unpaid services or products available!'");
		Возврат Истина;
	КонецЕсли;

	Возврат Ложь;

КонецФункции

&Вместо("ПроверитьРазрешениеВходаПолучитьОснованиеПосещ")
Функция PocketKey_ПроверитьРазрешениеВходаПолучитьОснованиеПосещ(ДатаФиксации, ЭтоВход, ОснованиеВхода, ВходНаОснованииЗапланированногоЗанятия, МножественныйВыборКлючейРазрешен, ТекущийКлиент, ДоступныеУслугиКлиента, ВремяПереодеванияДоЗанятия, ТабЗадолженностиКлиента, ТекстСообщения, КодОграничения)

	ВходРазрешен = Ложь;

	Если МножественныйВыборКлючейРазрешен = Ложь Тогда

		ТекущийКлиентВКлубе  = Документы.Посещение.КлиентВКлубе(ТекущийКлиент);

		Если ТекущийКлиентВКлубе = Истина Тогда

			КодОграничения = 101;
			ТекстСообщения = Нстр("ru='Вход клиента уже зафиксирован с другого рабочего места!'; en='Entrance client is already committed from another worker places!'");
			Возврат Ложь;

		КонецЕсли;

	КонецЕсли;

	Если Константы.РазрешатьВходКлиентовСЗадолженностью.Получить() = Истина Тогда

		Если ВходКлиентРазрешенПроверкаПоНастройкамРазрешения(ТекущийКлиент, ТекстСообщения) = Ложь Тогда

			КодОграничения = 100;
			Возврат Ложь;

		КонецЕсли;

	ИначеЕсли НЕ ТабЗадолженностиКлиента = Неопределено Тогда

		Если ПроверитьНаличиеЗадолженностеУКлиента(ТабЗадолженностиКлиента, ТекущийКлиент, ТекстСообщения) = Истина Тогда

			КодОграничения = 100;
			ТекстСообщения = Нстр("ru='Вход клиента запрещен. '; en='Entrance client is prohibited.'") + ТекстСообщения;
			Возврат Ложь;

		КонецЕсли;

	КонецЕсли;

	ИмеетсяОснованиеСНеобхАктивации = Ложь;
	ИмеетсяОснованиеСНеобхАктивацииЧленство = Ложь;

	//Проверяем доступность пакетам и членствам
	Если ОснованиеВхода = Неопределено Тогда

		Для Каждого ВрУровень ИЗ ДоступныеУслугиКлиента.ПолучитьЭлементы() Цикл

			ЭлементыНжУровня = ВрУровень.ПолучитьЭлементы();

			Для Каждого НжУровень ИЗ ЭлементыНжУровня Цикл
				
				// ++ ЛАА
				Попытка
					Если СтрНайти(Врег(Строка(НжУровень.Основание.Номенклатура.Наименование)), "ДКК") > 0 Тогда
						Продолжить;
					КонецЕсли;
				Исключение
				КонецПопытки;
				// -- ЛАА
				
				Если ТипЗнч(НжУровень.Основание) = Тип("ДокументСсылка.ЧленствоПакетУслуг") 
					И НжУровень.Основание.ТипЧленстваПакетаУслуг = Перечисления.ТипыНоменклатуры.АбонементЧленство
					И НжУровень.Основание.ОбязательноеНаличиеРезерваПриВходе = Ложь
					И НжУровень.Доступность = Истина Тогда

					ОснованиеВхода = НжУровень.Основание;
					ВходРазрешен = Истина;
					Прервать;

				ИначеЕсли ТипЗнч(НжУровень.Основание) = Тип("ДокументСсылка.ЧленствоПакетУслуг")
					И НжУровень.Основание.ТипЧленстваПакетаУслуг = Перечисления.ТипыНоменклатуры.АбонементПакетУслуг 
					И НжУровень.Основание.ВидЧленстваПакетаУслуг = Перечисления.ВидыЧленствПакетовУслуг.ОграниченПосещениями
					И НжУровень.Основание.ОбязательноеНаличиеРезерваПриВходе = Ложь
					И НжУровень.Доступность = Истина
					И НжУровень.КоличествоДоступныхПосещений > 0 Тогда 

					ОснованиеВхода = НжУровень.Основание;
					ВходРазрешен   = Истина;
					Прервать;

				ИначеЕсли ТипЗнч(НжУровень.Основание) = Тип("ДокументСсылка.ЧленствоПакетУслуг") 
					И НжУровень.Доступность = Ложь
					И НжУровень.Основание.ВидЧленстваПакетаУслуг = Перечисления.ВидыЧленствПакетовУслуг.ОграниченПосещениями
					И НжУровень.ТребуетсяАктивация = Истина Тогда

					ИмеетсяОснованиеСНеобхАктивацииЧленство = НжУровень.Основание.ТипЧленстваПакетаУслуг = Перечисления.ТипыНоменклатуры.АбонементЧленство;
					ИмеетсяОснованиеСНеобхАктивации = Истина;
					
				//+Трегубов
				ИначеЕсли ТипЗнч(НжУровень.Основание) = Тип("ДокументСсылка.ЧленствоПакетУслуг")
					И НжУровень.Основание.ТипЧленстваПакетаУслуг = Перечисления.ТипыНоменклатуры.АбонементПакетУслуг
					И НжУровень.Основание.ВидЧленстваПакетаУслуг = Перечисления.ВидыЧленствПакетовУслуг.ОграниченКоличеством
					И НжУровень.Доступность = Истина
					И НжУровень.Основание.ЗамораживатьОдновременноСЧленством = Ложь
					И НжУровень.Количество > 0 Тогда
					
					ОснованиеВхода = НжУровень.Основание;
					ВходРазрешен   = Истина;
				//-Трегубов
					
				КонецЕсли;	

			КонецЦикла;

			Если ЗначениеЗаполнено(ОснованиеВхода) Тогда
				Прервать;
			КонецЕсли;

		КонецЦикла;

	КонецЕсли;

	//Проверяем доступность по запланированным занятиям 
	//если нет достпноого пакета или членства
	Если ОснованиеВхода = Неопределено Тогда

		МассивТипыЗанятия = Новый Массив;
		МассивТипыЗанятия.Добавить(Перечисления.ТипыЗанятий.ПерсональноеЗанятие);
		МассивТипыЗанятия.Добавить(Перечисления.ТипыЗанятий.ГрупповоеЗанятие);
		МассивТипыЗанятия.Добавить(Перечисления.ТипыЗанятий.АрендаЗала);

		Если ЭтоВход = Истина Тогда

			//Выбираем все запланированные занятия на этот день 
			ЗанятияНаВремяПосещения = Документы.Занятие.ЗанятияЗаУказанныйПериод(НачалоДня(ДатаФиксации), КонецДня(ДатаФиксации)
			, МассивТипыЗанятия
			,Перечисления.СтатусыЗанятия.Запланировано,
			,,ТекущийКлиент).ВыгрузитьКолонку("Занятие");

		Иначе

			//Выбираем все занятия на этот день
			ЗанятияНаВремяПосещения = Документы.Занятие.ЗанятияЗаУказанныйПериод(НачалоДня(ДатаФиксации), КонецДня(ДатаФиксации), МассивТипыЗанятия,
			,,,ТекущийКлиент).ВыгрузитьКолонку("Занятие");

		КонецЕсли;

		Для Каждого Занятие ИЗ ЗанятияНаВремяПосещения Цикл

			ТаблицаСоставаЗанятия = Неопределено;
			Документы.Занятие.ИнициализироватьТекущийСоставЗанятия(, Занятие, ТаблицаСоставаЗанятия);	

			НайденныеСтр = ТаблицаСоставаЗанятия.НайтиСтроки(Новый Структура("Контрагент", ТекущийКлиент)); 
			Если НайденныеСтр.Количество() > 0 
				И (ЗначениеЗаполнено(НайденныеСтр[0].ОснованиеОплаты) ИЛИ ЭтоВход = Истина) Тогда

				// Проверяем на временную бронь у клиента.
				Если ЭтоВход = Истина 
					И НЕ ЗначениеЗаполнено(НайденныеСтр[0].ОснованиеОплаты) 
					И ЗначениеЗаполнено(НайденныеСтр[0].ДатаБронирования) 
					И Занятие.Номенклатура.ТребуетсяОснованиеДляЗаписи = Истина Тогда

					Продолжить;

				КонецЕсли;

				//Проверяем по времени, занятие не подходящее по времени не допускается
				
				// ++ ЛАА 	
				Условие1	= PocketKey_Модуль.PocketKey_УслугаЗанятияВСегментеДобавленияВремени(Занятие.Номенклатура, 1);
				Условие2	= PocketKey_Модуль.PocketKey_УслугаЗанятияВСегментеДобавленияВремени(Занятие.Номенклатура, 2);		
			   	ДопВремя	= 0;
				Если Условие1 ИЛИ Условие2 Тогда						
					 ДопВремя = 60 * 60;	
				КонецЕсли;
				// -- ЛАА	
				
				Если ЭтоВход = Ложь
					ИЛИ Занятие.ДатаВремяНачала <= (ДатаФиксации + (ВремяПереодеванияДоЗанятия  * 60) + ДопВремя) Тогда					

					ОснованиеОплаты = НайденныеСтр[0].ОснованиеОплаты;

					Если ТипЗнч(ОснованиеОплаты) = Тип("ДокументСсылка.ЧленствоПакетУслуг")
						И ОснованиеОплаты.ТипЧленстваПакетаУслуг = Перечисления.ТипыНоменклатуры.АбонементПакетУслуг
						И ОснованиеОплаты.ЗамораживатьОдновременноСЧленством = Истина
						И Документы.ЧленствоПакетУслуг.ПолучитьСписокАктивныхЧленств(ТекущийКлиент, ОснованиеОплаты, ДатаФиксации).Количество() = 0 Тогда

						Продолжить;

					ИначеЕсли ТипЗнч(ОснованиеОплаты) = Тип("ДокументСсылка.ЧленствоПакетУслуг")
						И ОснованиеОплаты.ТипЧленстваПакетаУслуг = Перечисления.ТипыНоменклатуры.АбонементЧленство Тогда

						СтруктураОграниченийВремениДействия = Документы.ЧленствоПакетУслуг.ПолучитьСтруктуруОграниченийВремениДействия(ОснованиеОплаты);

						Если СтруктураОграниченийВремениДействия.ОграниченоПоВремени = Истина
							И Не Документы.ЧленствоПакетУслуг.ЧленствоПакетУслугДоступноПоВремениДействия(СтруктураОграниченийВремениДействия.ВремяДействия, Занятие.ДатаВремяНачала, Занятие.ДатаВремяОкончания, Ложь) Тогда

							Продолжить;
						КонецЕсли;

					ИначеЕсли ТипЗнч(ОснованиеОплаты) = Тип("ДокументСсылка.ЧленствоПакетУслуг")
						И ОснованиеОплаты.ВидЧленстваПакетаУслуг = Перечисления.ВидыЧленствПакетовУслуг.ОграниченПосещениями
						И ОснованиеОплаты.ПосещенийНеЧаще > 0  
						И Документы.ЧленствоПакетУслуг.ИспользованиеЧПУОграниченоПоКоличествуПосещений(
						ОснованиеОплаты, ОснованиеОплаты.ПосещенийНеЧаще, ОснованиеОплаты.ПосещенийНеЧащеТип, ТекущийКлиент, ДатаФиксации) Тогда

						Продолжить;

					КонецЕсли;

					ВходРазрешен                           = Истина;
					ВходНаОснованииЗапланированногоЗанятия = Истина;
					ОснованиеВхода                         = ОснованиеОплаты;	

					Если ЗначениеЗаполнено(ОснованиеВхода) Тогда
						Прервать;
					КонецЕсли;

				КонецЕсли;	

			КонецЕсли;
		КонецЦикла;
	КонецЕсли;

	СообщениеОбОшибке = "";

	Если ПараметрыСеанса.ТекущаяСтруктурнаяЕдиница.ОграничениеПроходовПоQR 
		И Не РегистрыСведений.МедицинскиеСправкиКлиентов.НаличиеУКлиентаСправкиCOVID(ТекущийКлиент, ДатаФиксации, СообщениеОбОшибке) Тогда

		КодОграничения = 105;
		ТекстСообщения = СообщениеОбОшибке;
		Возврат Ложь;

	КонецЕсли;

	//Проверяем наличии мед справки
	Если ОснованиеВхода <> Неопределено
		И ТипЗнч(ОснованиеВхода) = Тип("ДокументСсылка.ЧленствоПакетУслуг") Тогда

		Если НаличиеУКлиентаТребуемойМедСправки(ТекущийКлиент, ОснованиеВхода, ДатаФиксации, СообщениеОбОшибке) = Ложь Тогда

			КодОграничения = 103;
			ТекстСообщения = СообщениеОбОшибке;
			Возврат Ложь;		

		КонецЕсли;

	ИначеЕсли Не ЗначениеЗаполнено(ОснованиеВхода)
		И ИмеетсяОснованиеСНеобхАктивации = Истина Тогда

		КодОграничения = 104;
		ТекстСообщения = Нстр("ru='Перед использованием необходимо активировать '; en='Before using is necessary activate'") + ?(ИмеетсяОснованиеСНеобхАктивацииЧленство, Нстр("ru='членство'; en='membership'"), Нстр("ru='пакет'; en='package'"));
		Возврат Ложь;

	КонецЕсли;	

	Возврат ВходРазрешен;

КонецФункции

&Вместо("ВыставитьШтрафКлиенту")
Процедура PocketKey_ВыставитьШтрафКлиенту(ТекущийКлиент, ТекущийКлюч, ТекущаяСтруктурнаяЕдиница)
	//#Удаление
	//ВыставитьШтрафПарковка(ТекущийКлиент, ТекущийКлюч, ТекущаяСтруктурнаяЕдиница);
	//#КонецУдаления
	ДанныеПоВходу = Документы.Посещение.ДанныеПоследнегоВходаКлиента(ТекущийКлиент, ТекущийКлюч, ТекущаяСтруктурнаяЕдиница, Истина);

	Если Не ЗначениеЗаполнено(ДанныеПоВходу) Тогда
		Возврат;
	КонецЕсли;

	Номенклатура 	 = ?(ТипЗнч(ДанныеПоВходу.Основание) = Тип("ДокументСсылка.ЧленствоПакетУслуг"), ДанныеПоВходу.Основание.Номенклатура, Неопределено);

	НастройкиШтрафов = РегистрыСведений.НастройкиШтрафов.ПолучитьНастройкиШтрафов(ТекущаяСтруктурнаяЕдиница, Номенклатура);

	Если НастройкиШтрафов.Количество() > 0 Тогда

		ТаблицаРазрешенногоВремени = РегистрыСведений.НастройкиШтрафов.ПолучитьТаблицуРазрешенногоВремениПосещения(ТекущаяСтруктурнаяЕдиница, ТекущийКлиент, ТекущийКлюч, ДанныеПоВходу);

		ТекущееВремя = ?(ЗначениеЗаполнено(ДанныеПоВходу.ДатаУхода), НачалоМинуты(ДанныеПоВходу.ДатаУхода), НачалоМинуты(ТекущаяДатаСеанса()));
		Задержка 	 = 0;

		//#Удаление
		////Определяем время, на которое задержался клиент
		//Для Каждого СтрокаТЧ Из ТаблицаРазрешенногоВремени Цикл

		//	Задержка = ?(СтрокаТЧ.ВремяКонца < ТекущееВремя, Цел((ТекущееВремя - СтрокаТЧ.ВремяКонца)/60), 0);

		//КонецЦикла;
		//#КонецУдаления

		//#Вставка 
		//++
		//Определяем время, на которое задержался клиент
		Для Каждого СтрокаТЧ Из ТаблицаРазрешенногоВремени Цикл
			
			Попытка
				
				ВремяОкончания = СтрокаТЧ.ВремяКонца + (Константы.ВремяПереодеванияПослеЗанятия.Получить() * 60); 
				Задержка = ?(ВремяОкончания < ТекущееВремя, Цел((ТекущееВремя - ВремяОкончания) / 60), 0);
				
				Если PocketKey_ИсключитьИзПроверкиНаШтраф(Номенклатура) Тогда
					Задержка = 0;
				КонецЕсли;
				
			Исключение
				Задержка = 0;
			КонецПопытки;
			
		КонецЦикла;
		//--
		//#КонецВставки

		Если Задержка > 0 Тогда

			МассивНоменклатуры 		 = Новый Массив;
			ВремяДополнительныхУслуг = Неопределено;

			Для Каждого СтрокаШтрафа Из НастройкиШтрафов Цикл

				Если СтрокаШтрафа.УчитыватьВремяДополнительныхУслуг Тогда

					Если ВремяДополнительныхУслуг = Неопределено Тогда

						МассивИсключаемыхНоменклатур = НастройкиШтрафов.ВыгрузитьКолонку("НоменклатураОграничения");
						ТаблицаУслуг 				 = ПолучитьДополнительныеУслуги(ТекущийКлиент, МассивИсключаемыхНоменклатур, ДанныеПоВходу.ДатаПрихода, ТекущееВремя);
						ВремяДополнительныхУслуг 	 = 0;

						Для Каждого СтрокаТЧ Из ТаблицаУслуг Цикл
							ВремяПереодевания 	 	 = ОбщегоНазначения.ПолучитьЗначениеРеквизитаНоменклатуры(СтрокаТЧ.Номенклатура, "ВремяПереодевания",, Ложь);
							ВремяДополнительныхУслуг = ВремяДополнительныхУслуг + СтрокаТЧ.НормаВремени + ?(ВремяПереодевания <> Неопределено, ВремяПереодевания, 0);
						КонецЦикла;

					КонецЕсли;

					ЗадержкаСДопУслугами = ?(ВремяДополнительныхУслуг >= Задержка, 0, Задержка - ВремяДополнительныхУслуг);

				Иначе
					ЗадержкаСДопУслугами = Задержка;
				КонецЕсли;

				ВремяЗадержки = Дата(1,1,1) + ?(СтрокаШтрафа.УчитыватьВремяДополнительныхУслуг, ЗадержкаСДопУслугами, Задержка) * 60;

				//Проверяем время действия и номенклатуру ограничения штрафа.
				Если ЗначениеЗаполнено(ВремяЗадержки)
					И (Не ЗначениеЗаполнено(СтрокаШтрафа.НачалоПериода) И Не ЗначениеЗаполнено(СтрокаШтрафа.КонецПериода)
					ИЛИ СтрокаШтрафа.НачалоПериода <= ВремяЗадержки И СтрокаШтрафа.КонецПериода >= ВремяЗадержки) Тогда

					ОграниченияВремениДействия = СтрокаШтрафа.ОграниченияВремениДействия.Получить();

					Если ЗначениеЗаполнено(ОграниченияВремениДействия) Тогда

						ТекущееВремяБезДаты = Дата(1,1,1) + (ТекущееВремя - НачалоДня(ТекущееВремя));
						ЭтоРабочийДень 		= ОбщегоНазначения.ЭтоРабочийДень(ТекущееВремя);

						Для Каждого СтрокаТЧ Из ОграниченияВремениДействия Цикл

							Если Документы.ЧленствоПакетУслуг.ОграничениеВремениДействияЧПУВключаетВыбраннуюДату(СтрокаТЧ.ДеньНедели, ТекущееВремя, ЭтоРабочийДень)
								И СтрокаТЧ.ВремяС < ТекущееВремяБезДаты
								И СтрокаТЧ.ВремяПо >= ТекущееВремяБезДаты Тогда

								ДобавитьСтруктуруНоменклатурыВМассив(СтрокаШтрафа, ЗадержкаСДопУслугами, МассивНоменклатуры);
								Прервать;
							КонецЕсли;

						КонецЦикла;

					Иначе
						ДобавитьСтруктуруНоменклатурыВМассив(СтрокаШтрафа, ЗадержкаСДопУслугами, МассивНоменклатуры);
					КонецЕсли;

				КонецЕсли;

			КонецЦикла;

			Если МассивНоменклатуры.Количество() > 0 Тогда
				//Выставляем штраф
				Реализация = РегистрыСведений.НастройкиШтрафов.ВыставитьШтрафКлиенту(ТекущаяСтруктурнаяЕдиница,ТекущийКлиент, МассивНоменклатуры, ДанныеПоВходу.Штраф);
				ДокПосещение = ДанныеПоВходу.Ссылка.ПолучитьОбъект();
				ДокПосещение.Штраф = Реализация;
				ДокПосещение.Записать(РежимЗаписиДокумента.Запись);

			ИначеЕсли ЗначениеЗаполнено(ДанныеПоВходу.Штраф) Тогда
				//Удаляем штраф
				РеализацияОбъект = ДанныеПоВходу.Штраф.ПолучитьОбъект();
				РеализацияОбъект.УстановитьПометкуУдаления(Истина);

				ДокПосещение = ДанныеПоВходу.Ссылка.ПолучитьОбъект();
				ДокПосещение.Штраф = Неопределено;
				ДокПосещение.Записать(РежимЗаписиДокумента.Запись);

			КонецЕсли;

		КонецЕсли;

	КонецЕсли;

КонецПроцедуры

// Функция, проверяет на исключение из проверки на штраф
// Основание - номенклатура
Функция PocketKey_ИсключитьИзПроверкиНаШтраф(Основание) Экспорт
	
	лТекст = "
	|ВЫБРАТЬ
	|	СоставСегментов.Сегмент КАК Сегмент,
	|	СоставСегментов.ОбъектСегмента КАК ОбъектСегмента
	|ИЗ
	|	РегистрСведений.СоставСегментов КАК СоставСегментов
	|ГДЕ
	|	СоставСегментов.Сегмент.Наименование = ""Исключить из проверки на штраф""
	|	И СоставСегментов.ОбъектСегмента = &Основание
	|";
	
	лЗапрос = Новый Запрос(лТекст);
	
	// Установка параметров.
	лЗапрос.УстановитьПараметр("Основание", Основание);
	
	Возврат НЕ лЗапрос.Выполнить().Пустой();
	
КонецФункции
